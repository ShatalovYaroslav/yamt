buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE'
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.5.0'
    id 'eclipse'
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'maven'
    id 'war'
    id "org.sonarqube" version "1.2"
}

apply plugin: 'spring-boot'

group = 'org.ow2.proactive'

bootRepackage {
    enabled = true
}

configurations {
    // The following module is excluded to avoid clashes when embedded inside the ProActive Scheduler
    all*.exclude module: 'spring-boot-starter-logging'

    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

coveralls {
    sourceDirs = allprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
}

ext {
    generatedSrcDir = "$projectDir/src/generated/java"
}

repositories {
    jcenter()
}

sourceSets {
    generated {
        java {
            srcDirs += generatedSrcDir
        }
    }

    main {
        java {
            srcDirs += generatedSrcDir
        }
    }

    integrationTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')

        compileClasspath = sourceSets.main.output + configurations.integrationTestCompile
        runtimeClasspath = output + compileClasspath + configurations.integrationTestRuntime
    }
}

springBoot {
    mainClass = 'org.ow2.proactive.' + packageLastPartName + '.Application'
}

war {
    baseName = projectName
    version = version
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'log4j') {
            details.useTarget "org.slf4j:log4j-over-slf4j:1.7.5"
        }
    }
}

dependencies {
    compile 'com.espertech:esper:5.2.0'  // before using Drools
//    compile (group: 'com.aol.microservices', name:'micro-grizzly-with-jersey', version:'0.62')
//    {
//        exclude module: 'org.apache.logging.log4j'
//    }
    compile 'org.glassfish.jersey.bundles.repackaged:jersey-guava:2.22.2'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'com.sun.jersey:jersey-server:1.19.1'
    compile 'com.sun.jersey:jersey-client:1.19.1'

    compile 'org.projectlombok:lombok:1.16.6'

    compile 'commons-fileupload:commons-fileupload:1.3.1'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'

    /*
        Spring boot 1.3.X relies on hibernate 4 which is not compatible
        with the version that is used with Scheduling project
     */
    compile('org.springframework.boot:spring-boot-starter-data-jpa:1.3.3.RELEASE')
//            {
//        exclude module: 'hibernate-entitymanager'
//    }
//    compile 'org.hibernate:hibernate-entitymanager:5.1.0.Final'
//    compile 'org.hibernate:hibernate-core:5.1.0.Final'
    compile 'org.springframework.boot:spring-boot-starter-log4j2:1.3.3.RELEASE'
    compile('org.springframework.boot:spring-boot-starter-web:1.3.3.RELEASE') {
        exclude module: 'spring-boot-starter-tomcat'  //should not be excluded if tested as standalone application
        exclude module: 'spring-boot-starter-validation'
    }
    compile 'org.springframework.hateoas:spring-hateoas:0.19.0.RELEASE'

    providedCompile 'org.springframework.boot:spring-boot-starter-tomcat:1.3.3.RELEASE'      //'provided' because supposed that it already used embedded server otherwise use 'compile'
    providedRuntime 'org.hsqldb:hsqldb:2.3.3'                                               //'provided' because supposed that it already used embedded server otherwise use 'runtime'

    compile 'io.springfox:springfox-spring-web:2.4.0'
    compile 'io.springfox:springfox-swagger2:2.4.0'
    compile 'io.springfox:springfox-swagger-ui:2.4.0'
    compile 'com.google.guava:guava:19.0'

    testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
    testCompile 'com.google.truth:truth:0.28'
    testCompile 'junit:junit:4.12'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:2.0.2'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.powermock:powermock-mockito-release-full:1.6.4'

    testCompile 'com.google.code.gson:gson:2.6.2'
    testCompile 'com.jayway.restassured:rest-assured:2.9.0'
    testCompile 'org.springframework.boot:spring-boot-starter-test:1.3.3.RELEASE'
}

test {

    apply plugin: 'org.sonarqube'
    apply plugin: 'jacoco'

    sonarqube {
        properties {
            property "sonar.projectName", projectName
            property "sonar.projectKey", "$project.group:$projectName"
            property "sonar.sources", "src/main/java"
            property 'sonar.jacoco.reportPath', "${buildDir}/jacoco/testDebug.exec"
            property 'sonar.junit.reportsPath', "${buildDir}/test-results"
        }
    }

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    classpath += sourceSets.test.runtimeClasspath

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoIntegrationTest.exec")
    }

    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    executionData test, integrationTest

    sourceSets project.sourceSets.main

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

clean {
    sourceSets.generated.java.srcDirs.each {
       srcDir -> delete srcDir
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

check.dependsOn integrationTest

jacocoTestReport.dependsOn check
tasks.coveralls.dependsOn jacocoTestReport

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

idea {
    module {
        testSourceDirs += file('src/integration-test/java')
        testSourceDirs += file('src/integration-test/resources')

        scopes.TEST.plus += [
                configurations.integrationTestCompile,
                configurations.integrationTestRuntime
        ]
    }
}
